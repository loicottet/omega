apply plugin: 'signing'

configurations {
    maven { extendsFrom archives }
    published { extendsFrom archives, signatures}
}

signing {
    required = false //{ gradle.taskGraph.hasTask(uploadPublished) && !version.endsWith("SNAPSHOT") }
    sign configurations.archives
}

repositories {
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/groups/staging' }
    mavenCentral()
}

uploadPublished {

    doFirst {
        // configure repositories in a doFirst so we can late bind the properties
        // checkPasswords()
        repositories {
            mavenDeployer {
                def repoUrl = getStringProperty('publishRepositoryUrl', 'https://nexus.gluonhq.com/nexus/content/repositories/releases/');
                configurePOM(pom)
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                snapshotRepository(url: "https://nexus.gluonhq.com/nexus/content/repositories/snapshots/") {
                    authentication userName: gluonNexusUsername, password: gluonNexusPassword
                }
                repository(url: repoUrl) {
                    authentication userName: gluonNexusUsername, password: gluonNexusPassword
                }
            }
        }
    }
    configuration = configurations.published
}

install {
    doFirst {
        configurePOM(repositories.mavenInstaller.pom)
    }
    configuration = configurations.published
}

private configurePOM(def pom) {
    pom.project {
        name "Gluon ${project.name.capitalize()}"
        description "Gluon ${project.name.capitalize()} library"
        url 'https://gluonhq.com/products/mobile/'
        modelVersion '4.0.0'
        packaging 'jar'
        organization {
            name 'Gluon'
            url 'https://gluonhq.com'
        }
    }
}

private String getStringProperty(String name, String defaultValue) {
    if (!hasProperty(name)) {
        return defaultValue
    }

    def propertyValue = getProperty(name)
    return propertyValue != null ? propertyValue.toString().trim() : defaultValue
}